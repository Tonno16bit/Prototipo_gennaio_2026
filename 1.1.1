#include <WiFi.h>
#include <esp_now.h>

const int pot1Pin = 0; // ADC1_CH0 
const int pot2Pin = 1; // ADC1_CH1 
const int pot3Pin = 2; // ADC1_CH2

typedef struct {
  int a;
  int b;
  int c;
} payload_t;

uint8_t receiverMAC[] = {0x48, 0x31, 0xB7, 0x55, 0xE6, 0xB8};


void OnSent(const wifi_tx_info_t *info, esp_now_send_status_t status) {
  Serial.print("Send status: ");
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "OK" : "FAIL");
}

int smooth(int newValue, int oldValue) {
  return (oldValue * 0.7) + (newValue * 0.3);  // 70% old, 30% new
}

int pot1Smooth = 0;
int pot2Smooth = 0;
int pot3Smooth = 0;

void setup() {
  Serial.begin(115200);
  // ADC configuration
  analogReadResolution(12);          // 0–4095
  analogSetAttenuation(ADC_11db);    // Full 0–3.3V range
  //pin configuration
  pinMode(pot1Pin, INPUT);
  pinMode(pot2Pin, INPUT);
  pinMode(pot3Pin, INPUT);
  //espnow configuration
  WiFi.mode(WIFI_STA);
  Serial.print("My MAC: ");
  Serial.println(WiFi.macAddress());

  if (esp_now_init() != ESP_OK) {
    Serial.println("ESP-NOW init failed");
    return;
  }

  esp_now_register_send_cb(OnSent);

  esp_now_peer_info_t peer{};
  memcpy(peer.peer_addr, receiverMAC, 6);
  peer.channel = 0;
  peer.encrypt = false;

  if (esp_now_add_peer(&peer) != ESP_OK) {
    Serial.println("Failed to add peer");
    return;
  }
}

void loop() {
  int pot1 = analogRead(pot1Pin);
  int pot2 = analogRead(pot2Pin);
  int pot3 = analogRead(pot3Pin);

  // Apply smoothing
  pot1Smooth = smooth(pot1, pot1Smooth);
  pot2Smooth = smooth(pot2, pot2Smooth);
  pot3Smooth = smooth(pot3, pot3Smooth);

  payload_t data = {
    pot1Smooth,
    pot2Smooth,
    pot3Smooth
  };
  //send data
  esp_now_send(receiverMAC, (uint8_t*)&data, sizeof(data));
  delay(50);
}
